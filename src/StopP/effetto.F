	PROGRAM EFFETTO
 
C		FA I CONTI CON  L'INTEGRALE SINGOLO
C		NON FUNZIONA SE IL RECOIL SI FERMA NEL FERROMANETICO
C		STOPPING POWERS CALCOLATI DA ZIEGLER

	logical*1 QUOTE,KINE
	REAL MBNP,MASS1,MASS2,MASS3,MASS4,MASS12,MASS34
	INTEGER*4 ZPROJ,ZTARG,ZREC,ZLIGHT
	INTEGER*4 APROJ,ATARG,AREC,ALIGHT
	DIMENSION DENARR(2),NATOMS(2),THICK(2),CTHICK(2)
	DIMENSION ZARR(20,2),AARR(20,2),ANARR(20,2)
	CHARACTER*10 NAMTAR,NAMARR,NAMNUCL*2

	DATA CCMSEC/29979250000./		! c  (cm/sec)
	DATA XMNKEV/931478./			! Mn (keV)
	DATA PIGRE/3.141592654/			! pi greco
	DATA DEGRAD/57.29577951/		! 180/pigreco
	DATA ALN2/0.693147181/			! ln(2)
	DATA HTCSUE2/137./			! e2/h_c=1/137
	DATA XMUNSUHT/4798.4/			! muN/h_ (rad/Gauss/sec)

	DATA FACPT,FACLT,FACRT,FACRF/1.,1.,1.,1./
	DATA NATOMS /1,1/
	DATA NDIV,IHOST,IFIEL/2,0,0/
	DATA KINE /.TRUE./
	DATA THMIND,THMAXD,DNNTH/140.,180.,2./

1	WRITE(5,'(//20X,
	1	'' PROGRAM EFFETTO'',//,2X,''Reaction definition''/)')
 
C		  INPUT    REACTION
 
	CALL IIN2('$Z,A   of projectile : ',ZPROJ,APROJ)
	IF(ZPROJ.LE.0 .OR. APROJ.LE.0) GOTO 1
	CALL XMASS(ZPROJ,APROJ,XMASS1,DMASS1)
	MASS1=XMASS1/XMNKEV+APROJ
 
	CALL IIN2('$Z,A   of target : ',ZTARG,ATARG)
	IF(ZTARG.LE.0. .OR. ATARG.LE.0) GOTO 1
	CALL XMASS(ZTARG,ATARG,XMASS2,DMASS2)
	MASS2=XMASS2/XMNKEV+ATARG
 
	CALL IIN2('$Z,A   of recoil : ',ZREC,AREC)
	IF(ZREC.LE.0. .OR. AREC.LE.0) GOTO 1
	CALL XMASS(ZREC,AREC,XMASS4,DMASS4)
	MASS4=XMASS4/XMNKEV+AREC
 
	ZLIGHT=ZPROJ+ZTARG-ZREC
	ALIGHT=APROJ+ATARG-AREC
	IF(ZLIGHT.LT.0 .OR. ALIGHT.LT.0) GOTO 1
	CALL XMASS(ZLIGHT,ALIGHT,XMASS3,DMASS3)
	MASS3=XMASS3/XMNKEV+ALIGHT
 
	MASS12=MASS1+MASS2
	MASS34=MASS3+MASS4
	QVAL=XMASS1+XMASS2-(XMASS3+XMASS4)
	DQVAL=SQRT(DMASS1**2+DMASS2**2+DMASS4**2+DMASS3**2)
	WRITE(5,'('' Q-value='',F10.2,''  keV'')') QVAL

	CALL RIN1('$Excitation Energy (keV) : ',EXENER)
	QVALO=QVAL-EXENER

	CALL RIN1('$T1/2 of recoil (fsec){ <0 ==> tau } : ',T12F)
	IF(T12F.EQ.0.) GOTO 1
	if(T12F.LT.0) T12F=-T12F*ALN2
	TIMLIF=T12F/ALN2*1.0E-15

5	IF(MASS3.LE.0.) THEN
		NNTH=0
		THM=0.
		THMAX=0.
		GOTO 10
	ENDIF

	CALL RIN3('$Min.,Max. angle for light particle (DEG.) [,#step] : ',
	1	THMIND,THMAXD,DNNTH)
	IF(DNNTH.LT.1) DNNTH=1
	IF(THMAXD.GT.180.) THMAXD=180.
	IF(THMAXD.LT.0.) THMAXD=0.
	IF(THMIND.GT.THMAXD) THMIND=THMAXD
	IF(THMIND.LT.0.) THMIND=0.
	THMAX=THMAXD/DEGRAD
	THMIN=THMIND/DEGRAD
	NNTH=DNNTH
	DTH=(THMAX-THMIN)/NNTH
	IF(DTH.EQ.0.) NNTH=0
	THM=THMAX

C	 INPUT FOR TARGET

10	CALL CHINA('$Target material symbol  ',NAMTAR)
	XX=NATOMS(1)
	CALL RIN2('$Target density(g/cm3) [,#of different atoms] : ',
	1		DENARR(1),XX)
	IF(DENARR(1).LE.0.) GOTO 1
	IF(XX.EQ.1.) THEN
		NATOMS(1)=1
		ZARR(1,1)=ZTARG
		AARR(1,1)=ATARG
		ANARR(1,1)=1.
	ELSE
		NATOMS(1)=MIN(20.,MAX(1.,XX))
		DO II=1,NATOMS(1)
			WRITE(5,'('' Z,A,#atoms/molecule of target #'',I3,$)') II
			CALL RIN3('$',ZARR(II,1),AARR(II,1),ANARR(II,1))
			IF(ZARR(II,1).LE.0.) GOTO 1
			IF(ANARR(II,1).LE.0.) ANARR(II,1)=1.
		ENDDO
	ENDIF

	XX=NDIV
	CALL RIN2('$Target thickness{ >0==>mmg/cm2 <0==>micron }[,#step] : ',
	1	THICK(1),XX)
	IF(XX.LT.1) XX=1
	NDIV=XX
	IF(THICK(1).EQ.0.) GOTO 1
	IF(THICK(1).LT.0.) THICK(1)=-THICK(1)*DENARR(1)*100.
	CTHICK(1)=THICK(1)/DENARR(1)*1.0E-6

	IHOSTO=IHOST
	CALL IIN1('$Ferromagnetic host  IRON(1) or GADOLINIUM(2) : ',IHOST)
	IF(IHOST.NE.1 .AND. IHOST.NE.2) GOTO 1 

	IF(IHOST.EQ.1 .AND. IHOST.NE.IHOSTO) THEN
		NAMARR='IRON'		! FERROMAGNETIC   IRON
		DENARR(2)=7.866
		NATOMS(2)=1
		ZARR(1,2)=26.
		AARR(1,2)=55.8
		ANARR(1,2)=1.
		APRIM=17.	! =96.7*1752(=MBNP) /10000
		PV=0.45
		PZ=1.1
		ALFA=19.
		BETA=0.12
	ELSE IF(IHOST.EQ.2 .AND. IHOST.NE.IHOSTO) THEN
		NAMARR='GADOLINIUM'	! FERROMAGNETIC   GADOLINIUM
		DENARR(2)=7.9
		NATOMS(2)=1
		ZARR(1,2)=64.
		AARR(1,2)=158.
		ANARR(1,2)=1.
		APRIM=17.	! =96.7*1752(=MBNP) /10000
		PV=0.45
		PZ=1.1
		ALFA=27.
		BETA=0.135
	ENDIF

	CALL RIN1(
	1 '$Ferromagn. thickness { >0==>mmg/cm2  <0==>micron } : ',THICK(2))
	IF(THICK(2).EQ.0.) GOTO 1
	IF(THICK(2).LT.0.) THICK(2)=-THICK(2)*DENARR(2)*100.
	CTHICK(2)=THICK(2)/DENARR(2)*1.0E-6

	KINE=.NOT.QUOTE('$Skip the kinematics (Y/N) ? ')
	IF(KINE) CALL RIN1('$Projectile energy(keV) : ',EPROJ)
	IF(.NOT.KINE) CALL RIN1('$Recoil energy(keV) : ',EPROJ)
	IF(EPROJ.LE.0.) GOTO 1

C		TRANSIENT FIELD PARAMETRISATION

	CALL IIN1('$TF parametrisation Rutgers(1) or  Chalk-River(2) : ',IFIEL)
	IF(IFIEL.NE.1 .AND. IFIEL.NE.2) GOTO 1

	IF(IFIEL.EQ.1) THEN
		WRITE(5,'('' Magnetic FIELD parametrisation is ''
	1	/'' B(v) = A * Zrec**Pz * (v/v0)**Pv    Tesla''
	1	/'' presetted values are '',3F10.2)') APRIM,PZ,PV
		CALL RIN3('$Input A,Pz,Pv : ',APRIM,PZ,PV)
		IF(APRIM.LE.0.) GOTO 1
	ELSE
		WRITE(5,'('' Magnetic FIELD parametrisation is ''
	1	/'' B(v) = Alfa * Zrec * (v/v0) * EXP( -Beta * (v/v0))   Tesla''
	1	/'' presetted values are '',2G)') ALFA,BETA
		CALL RIN2('$Input Alfa,Beta : ',ALFA,BETA)
		IF(ALFA.EQ.0.) GOTO 1
	ENDIF

	CALL RIN1('$Fattore per lo stopping del proj.  in targ.   : ',FACPT)
	IF(FACPT.LT.0) FACPT=1
	CALL RIN1('$Fattore per lo stopping del light  in targ.   : ',FACLT)
	IF(FACLT.LT.0) FACLT=1
	CALL RIN1('$Fattore per lo stopping del recoil in targ.   : ',FACRT)
	IF(FACRT.LT.0) FACRT=1
	CALL RIN1('$Fattore per lo stopping del recoil in ferrom. : ',FACRF)
	IF(FACRF.LT.0) FACRF=1

	WRITE(7,6000)
6000	FORMAT(1H1,25X,'**** TRANSIENT FIELD EFFECT CALCULATIONS ****'/)
	WRITE(7,6005)
6005	FORMAT(10X,'INPUT VALUES'/)
	IF(KINE) WRITE(7,6010) APROJ,NAMNUCL(ZPROJ),EPROJ/1000.
	IF(.NOT.KINE) WRITE(7,6010) APROJ,NAMNUCL(ZPROJ)
6010	FORMAT(' Projectile      ',I6,A2,10X,'Energy=',F9.3,'  MeV')
	WRITE(7,6020) ATARG,NAMNUCL(ZTARG)
6020	FORMAT(' Target          ',I6,A2)
	WRITE(7,6030) ALIGHT,NAMNUCL(ZLIGHT)
6030	FORMAT(' Light particle  ',I6,A2)
	IF(KINE) WRITE(7,6040) AREC,NAMNUCL(ZREC)
	IF(.NOT.KINE) WRITE(7,6040) AREC,NAMNUCL(ZREC),EPROJ/1000.
6040	FORMAT(' Recoil nucleus  ',I6,A2,10X,'Energy=',F9.3,'  MeV')
	WRITE(7,6050) QVAL,EXENER,T12F
6050	FORMAT(/' Qvalue=',F8.2,10X,'Excitation energy=',F8.2,'  keV',
	17X,' T1/2 =',F10.2,'  fsec')
	WRITE(7,6070) NAMTAR,THICK(1),DENARR(1)
6070	FORMAT(/' Target material         ',A
	1,10X,' Thickness=',F10.2,'  mmg/cm2',
	8X,' Density=',F8.2,'  g/cm3')
	IF(NATOMS(1).NE.1 .OR. ZARR(1,1).NE.ZTARG
	1   .OR. AARR(1,1).NE.ATARG) THEN
		DO II=1,NATOMS(1)
			WRITE(7,6090) ANARR(II,1),ZARR(II,1),AARR(II,1)
6090			FORMAT(' Target element #',F4.0,'   Z=',F4.0,'  A=',F6.2)
			ENDDO
	ENDIF
	WRITE(7,6110) NAMARR,THICK(2),DENARR(2)
6110	FORMAT(/' Ferromagnetic layer     ',A,10X,' Thickness=',
	1	F10.2,'  mmg/cm2',8X,' Density=',F8.2,'  g/cm3')
	IF(IFIEL.EQ.1) THEN
		WRITE(7,6120) APRIM,PZ,PV
6120		FORMAT(/' B(v) = ',F6.2,
	1	' * Zrec**',F5.3,' * (v/v0)**',F5.3,'  Tesla')
	ELSE
		WRITE(7,6121) ALFA,-BETA
6121		FORMAT(/' B(v) = ',F6.2,' * Zrec * (v/v0) * EXP(',
	1	F6.3,' * (v/v0))  Tesla')
	ENDIF

	WRITE(7,6137) FACPT,FACLT,FACRT,FACRF
6137	FORMAT(/' Fattori moltiplicativi per lo stopping power',5X,
	1 'Proj.-Targ.=',F6.3,3X,'Light-Targ.=',F6.3,5X,
	1 'Rec.-Targ.=',F6.3,3X,'Rec.-Ferrom.=',F6.3)

	IF(QUOTE('$Summary of stopping powers (Y/N) ? ')) THEN
		WRITE(6,6139)
		WRITE(7,6139)
6139	FORMAT(/' Energy(MeV)',5X,17('-'),' DEDX(keV/(mmg/cm2)) ',17('-'),/
	1	 18X,'Proj.-Targ.    Light-Targ.    Rec.-Targ.     Rec.-Ferr.')
		DO XXE=0,EPROJ,5000
			XX=XXE
			IF(XX.LT.1000) XX=1000
			SPW1=STPW(XX,ZPROJ,APROJ,NATOMS(1),
	1			ZARR(1,1),AARR(1,1),ANARR(1,1))*FACPT
			SPWL1=STPW(XX,ZLIGHT,ALIGHT,NATOMS(1),
	1			ZARR(1,1),AARR(1,1),ANARR(1,1))*FACLT
			SPW2=STPW(XX,ZREC,AREC,NATOMS(1),
	1			ZARR(1,1),AARR(1,1),ANARR(1,1))*FACRT
			SPW3=STPW(XX,ZREC,AREC,NATOMS(2),
	1			ZARR(1,2),AARR(1,2),ANARR(1,2))*FACRF
			WRITE(6,6141) XX/1000,SPW1,SPWL1,SPW2,SPW3
			WRITE(7,6141) XX/1000,SPW1,SPWL1,SPW2,SPW3
6141			FORMAT(F11.3,2X,4F15.4)
		ENDDO
	ENDIF

	WRITE(7,'(///25X,9(''*''),'' SUMMARY OF CALCULATIONS '',9(''*''),//)')
	WRITE(7,'(
	1''    React.Point  Proj.Ener.  Detect.Part.  Recoil  '',
	1''  Time(fs)   Energy(keV)   V/V0    FIELD(kG)'',
	1''   Teta/g(mr)   Teff(fs)   Beff(kG)''/
	1''     (mmg/cm2)     (keV)      Ener\Teta  Ener\Teta '',
	1''   In\Out      In\Out     In\Out    In\Out'')')


	DTHICK=THICK(1)/NDIV
	JJDIV=0

	DO 1000 IIDIV=1,NDIV+1
	TAR1=IFIX(DTHICK*(IIDIV-1)+0.5)
	TAR2=THICK(1)-TAR1

C	   PROJECTILE SLOWS DOWN IN THE TARGET

	EPT=EPROJ
	IF(.NOT.KINE) GOTO 1999
	IF(TAR1.EQ.0.) GOTO 1011
	XX2=TAR1
	DXX2=1
	IF(XX2.GT.500) THEN
		DXX2=XX2/500
		XX2=500
	ENDIF
	DO II=1,XX2
		STOP=STPW(EPT,ZPROJ,APROJ,NATOMS(1),
	1		ZARR(1,1),AARR(1,1),ANARR(1,1))*FACPT
		EPT=EPT-STOP*DXX2
		IF(EPT.LE.10) THEN
			WRITE(6,'('' PROJECTILE STOPS IN THE TARGET'')')
			GOTO 1000
		ENDIF
	ENDDO
1011	ETOT=EPT+QVALO

1999	DO 2000 IITH=1,NNTH+1
	IF(.NOT.KINE) THEN
		ERECO=EPROJ
		ERT=EPROJ
		T0=0
		COSCSI=1
		NSOLU=0
		GOTO 1021
	ENDIF

	IF(MASS3.LE.0.) THEN
		ERECO=ETOT*MASS1/MASS4
		CSI=0.
		NSOLU=0
		GOTO 993
	ENDIF

C		    KINEMATICS

	CALL CINEMA(MASS1,MASS2,MASS3,MASS4,QVALO,EPT,THM,
	1	ENED1,THU1,ENEU1,ENED2,THU2,ENEU2,THCMD1,THCMD2)

	IF(ENED1.LE.0 .AND. ENED2.LE.0) THEN
		THL=THM*DEGRAD
		WRITE(5,'('' No light particle at '',F8.2,''  DEG.'')')THL
		NSOLU=0
		GOTO 2999
	ENDIF 

	NSOLU=1
	IF(ENED1.GT.0 .AND. ENED2.GT.0) NSOLU=2

	IF(ENED1.GT.0) THEN
		ELIGHT=ENED1
		ERECO=ENEU1
		CSI=THU1
		GOTO 993
	ENDIF

988	IF(ENED1.LT.0 .OR. NSOLU.EQ.-1) THEN
		ELIGHT=ENED2
		ERECO=ENEU2
		CSI=THU2
		GOTO 993
	ENDIF
 
993	COSCSI=COS(CSI)
	IF(COSCSI.LE.0.) THEN
		THL=CSI*DEGRAD
		WRITE(5,'('' Recoil at '',F8.2,
	1		''  DEG. does not reach the ferromagnetic'')')THL
		GOTO 2999
	ENDIF
	
C	   LIGHT PARTICLE SLOWS DOWN IN THE TARGET

	DXCM=CTHICK(1)/THICK(1)
	ERLT=ELIGHT
	IF(TAR1.EQ.0. .OR. COS(THM).GE.0) GOTO 1019 ! SE VA IN AVANTI
	XX2=-TAR1/COS(THM)
	DXX2=1
	IF(XX2.GT.500) THEN
		DXX2=XX2/500
		XX2=500
	ENDIF
	DO II=1,XX2
		STOP=STPW(ERLT,ZLIGHT,ALIGHT,NATOMS(1),
	1		ZARR(1,1),AARR(1,1),ANARR(1,1))*FACLT
		ERLT=ERLT-STOP*DXX2
		IF(ERLT.LE.10) THEN
			WRITE(6,'('' Light particle stops in target'')')
			GOTO 2999
		ENDIF
	ENDDO

C	   RECOIL ION SLOWS DOWN IN THE TARGET

1019	DXCM=CTHICK(1)/THICK(1)
	ERT=ERECO
	VRT=SQRT(2*ERT/(MASS4*XMNKEV))*CCMSEC
	T0=0
	IF(TAR2.EQ.0.) GOTO 1021
	XX2=TAR2/COSCSI
	DXX2=1
	IF(XX2.GT.500) THEN
		DXX2=XX2/500
		XX2=500
	ENDIF
	DO II=1,XX2
		STOP=STPW(ERT,ZREC,AREC,NATOMS(1),
	1		ZARR(1,1),AARR(1,1),ANARR(1,1))*FACRT
		ERT=ERT-STOP*DXX2
		IF(ERT.LE.10) THEN
			WRITE(6,'('' Recoil stops in target'')')
			GOTO 2999
		ENDIF
		VRT1=SQRT(2*ERT/(MASS4*XMNKEV))*CCMSEC
C		DT=LOG(VRT1/VRT)/(VRT1-VRT)*DXCM*DXX2
		DT=DXCM/((VRT+VRT1)/2)*DXX2
		T0=T0+DT
		VRT=VRT1
	ENDDO

C	    RECOIL ION SLOWS DOWN IN FERROMAGNETIC

1021	CONTINUE
	DXCM=CTHICK(2)/THICK(2)
	EIN=ERT
	ERF=EIN
	VRF=SQRT(2*ERF/(MASS4*XMNKEV))*CCMSEC
	T1=T0
	IF(IFIEL.EQ.1) FIELD=FIELDR(APRIM,ZREC,PZ,VRF,PV)
	IF(IFIEL.NE.1) FIELD=FIELDC(ALFA,ZREC,VRF,BETA)
	FIELDIN=FIELD
	XX2=THICK(2)/COSCSI
	DXX2=1
	IF(XX2.GT.500) THEN
		DXX2=XX2/500
		XX2=500
	ENDIF
	BEFF1=0		! QUANTITA` EFFFICACI CALCOLATE PASSO-PASSO
	VEFF1=0
	DO II=1,XX2
		STOP=STPW(ERF,ZREC,AREC,NATOMS(2),
	1		ZARR(1,2),AARR(1,2),ANARR(1,2))*FACRF
		ERF=ERF-STOP*DXX2
		IF(ERF.LE.10) THEN
			WRITE(6,'(/'' Recoil stops in ferromagnetic'')')
			ERF=0
			FIELD=0
			GOTO 623
		ENDIF
		VRF1=SQRT(2*ERF/(MASS4*XMNKEV))*CCMSEC
C		DT=LOG(VRF1/VRF)/(VRF1-VRF)*DXCM*DXX2
		DT=DXCM/((VRF+VRF1)/2)*DXX2
		T1=T1+DT
		VRF=VRF1
		IF(IFIEL.EQ.1) FIELD=FIELDR(APRIM,ZREC,PZ,VRF,PV)
		IF(IFIEL.NE.1) FIELD=FIELDC(ALFA,ZREC,VRF,BETA)
		EXPDT=EXP(-T1/TIMLIF)*DT
		BEFF1=BEFF1+FIELD*EXPDT
		VEFF1=VEFF1+VRF*EXPDT
	ENDDO

623	EOUT=ERF
	FIELDOUT=FIELD

	SUM=BEFF1*XMUNSUHT*1000.

	VV01=SQRT(2.*EIN /MASS4/XMNKEV)*HTCSUE2
	VV02=SQRT(2.*EOUT/MASS4/XMNKEV)*HTCSUE2

	XF1=FIELDIN/1000.
	XF2=FIELDOUT/1000.

	T0P=T0*1.0E15
	T1P=T1*1.0E15

	TEFF1=TIMLIF*(EXP(-T0/TIMLIF)-EXP(-T1/TIMLIF))
	BEFF =BEFF1/TEFF1/1000
	VEFF1=VEFF1/TIMLIF/CCMSEC*HTCSUE2
	VEFF1=(VV01*EXP(-T0/TIMLIF)+VV02*EXP(-T1/TIMLIF)+VEFF1)/2
	VEFF1=VEFF1/EXP(-T0/TIMLIF)
	TEFF =TEFF1*1.0E15

	THL=THM*DEGRAD
	THR=CSI*DEGRAD

	IF(NSOLU.NE.-1) JJDIV=JJDIV+1
	JJW=JJDIV
	IF(NSOLU.EQ.-1) JJW=-JJW
	WRITE(5,626)JJW,TAR1,EPT,ERLT,THL,ERECO,THR,
	1	T0P,T1P,EIN,EOUT,VV01,VV02,VEFF1,XF1,XF2,SUM,TEFF,BEFF

626	FORMAT(/
	1 I5   '  Reaction at       ',F9.2,'  mmg/cm2 in the target'
	1/'  Projectile energy      ',F9.1,'  keV'
	1/'  Detected part. energy  ',F9.1,'  keV   at',F9.2,'  DEG.'
	1/'  Recoil initial energy  ',F9.1,'  keV   at',F9.2,'  DEG.'
	1/'  FIELD acting between   ',F9.2,'    and   ',F9.2,'  fsec'
	1/'  Energy in backing from ',F9.1,'    to    ',F9.1,'  keV'
	1/'  Velocity V/V0 from     ',F9.3,'    to    ',F9.3,4X,'<',F5.2,' >'
	1/'  Trans. FIELD  from     ',F9.2,'    to    ',F9.2,'  kGauss'
	1/'  Dteta=',F8.3,' mrad/g   Teff=',F8.2,' fs.  Beff=',F9.1,' kGauss')

	WRITE(7,6200)
	1 JJW,TAR1,EPT,ERLT,ERECO,T0P,EIN,VV01,XF1,SUM,TEFF,BEFF,
	1		THL,THR,T1P,EOUT,VV02,XF2
6200	FORMAT(/I3,
	1 F11.1,F12.1,F12.1,F11.1,F11.2,F13.1,F9.2,F12.1,F11.3,F12.2,F11.1/
	1         27X,F12.1,F11.1,F11.2,F13.1,F9.2,F12.1,F11.3,F12.2,F11.1)

2999	IF(.NOT.KINE) GOTO 1
	IF(NSOLU.EQ.2) THEN
		NSOLU=-1
		GOTO 988
		ENDIF
2000	THM=THM-DTH
	THM=THMAX

1000	CONTINUE

	GOTO 1

	END

	FUNCTION STPW(EP,IZP,IAP,NATOMS,Z,A,ATOM)

	DIMENSION Z(NATOMS),A(NATOMS),ATOM(NATOMS)

	ZP=IZP
	AP=IAP
	ASUM=0
	DO N=1,NATOMS
		ASUM= ASUM + A(N)*ATOM(N)
	ENDDO
	STPOW=0
	IF(EP.LE.0) GOTO 10

	DO N=1,NATOMS
	 	CALL ZIEGLERCH(ZP,AP,Z(N),A(N),EP,STE,IFLAG,C1,C2,STNUC,STLSS)
		IF(IFLAG.NE.1) THEN
			WRITE(5,'('' Stopping coefficients for Z='',
	1		I2,'' - A='',I2,'' not found'')') Z(N),A(N)
			STOP
			ENDIF
		STPOW=STPOW+(STE+STNUC)*ATOM(N)
	ENDDO

C	STTOT = STPOW in unita` eV/[E15Atomi/cm3]
C	STTOT =.6022*STPOW/ASUM in unita` keV/mmg/cm2

10	STPW=.6022*STPOW/ASUM

	RETURN

	END

	FUNCTION FIELD

	DATA CCMSEC/29979250000./		! c  (cm/sec)
	DATA HTCSUE2/137./			! e2/h_c=1/137

	ENTRY FIELDR(A,IZ,PZ,V,PV,XF)

	VV0=V/CCMSEC*HTCSUE2
	FIELDR=A * IZ**PZ * VV0**PV * 10000.
	RETURN

	ENTRY FIELDC(A,IZ,V,B)

	VV0=V/CCMSEC*HTCSUE2
	FIELDC=A * IZ * VV0 * EXP( -B*VV0 )*10000.
	RETURN

	END
